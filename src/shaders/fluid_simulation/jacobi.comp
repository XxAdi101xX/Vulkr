/* Copyright (c) 2022 Adithya Venkatarao
 *
 * Permission is hereby granted, free of charge, to any person obtaining
 * a copy of this software and associated documentation files (the
 * "Software"), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to
 * permit persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

#version 460
#extension GL_EXT_debug_printf : enable

layout(constant_id = 0) const uint workGroupSize = 64;
layout(local_size_x_id = 0) in; // Maps to the constant_id = 0 specialization constant
layout(constant_id = 1) const uint fluidVelocityBufferWidth = 1;
layout(constant_id = 2) const uint fluidVelocityBufferHeight = 1;
layout(constant_id = 3) const float alpha = 0.0001f;
layout(constant_id = 4) const float reciprocalBeta = 0.25f;

// layout(set = 0, binding = 0) uniform sampler2D fluidVelocityInputTextureImage;
layout(set = 0, binding = 1) uniform sampler2D fluidVelocityDivergenceInputTextureImage;
layout(set = 0, binding = 2) uniform sampler2D fluidPressureInputTextureImage;
// layout(set = 0, binding = 3) uniform sampler2D fluidDensityInputTextureImage;
layout(set = 1, binding = 0, rgba32f) uniform writeonly image2D fluidSimulationOutputTextureImage;

vec2 gridSize = vec2(fluidVelocityBufferWidth, fluidVelocityBufferHeight);

// We are iteratively solving Ax = b where x and b are both fluidVelocityInputTextureImage
void main() 
{
	uint globalInvocationId = gl_GlobalInvocationID.x;
	if (globalInvocationId >= fluidVelocityBufferWidth * fluidVelocityBufferHeight) return;

	ivec2 imageIndex;
	imageIndex.x = int(mod(globalInvocationId, fluidVelocityBufferWidth));
	imageIndex.y = int(globalInvocationId / fluidVelocityBufferWidth);
	vec2 screenSpaceCoords = imageIndex + 0.5f;
	vec2 uv = screenSpaceCoords / gridSize;

    vec2 xOffset = vec2(1.0f / gridSize.x, 0.0f);
    vec2 yOffset = vec2(0.0f, 1.0f / gridSize.y);

	// The pressure texture is the x component of the jacobi iterations, which is updated each iteration
	float xRight = texture(fluidPressureInputTextureImage, uv + xOffset).x;
	float xLeft = texture(fluidPressureInputTextureImage, uv - xOffset).x;
	float xTop = texture(fluidPressureInputTextureImage, uv + yOffset).x;
	float xBot = texture(fluidPressureInputTextureImage, uv - yOffset).x;

	// velocityDivergence texture is the b component
	float bCenter = texture(fluidVelocityDivergenceInputTextureImage, uv).x;

	vec4 jacobiIteration = vec4((xLeft + xRight + xBot + xTop + alpha * bCenter) * reciprocalBeta, 0.0f, 0.0f, 1.0f);
	imageStore(fluidSimulationOutputTextureImage, imageIndex, jacobiIteration);
}